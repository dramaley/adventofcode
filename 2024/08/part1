#!/usr/bin/perl

use 5.8.1;
use strict;
use warnings 'all';

our (%antennas, $max_row, $max_col);
{   # Read input into an array so it is easier to determine size
    my (@lines) = map { chomp; $_; } <>;
    $max_row = $#lines;
    $max_col = length($lines[0]) - 1;

    # Parse input
    for my $i (0..$max_row) {
        while ($lines[$i] =~ m/(?=([[:alnum:]]))/g) {
            push @{$antennas{$1}}, [ $i, pos($lines[$i]) ];
        }
    }
}

my %antinodes;
foreach my $val (values %antennas) {
    foreach my $i (0..$#$val-1) {
        my $one = $$val[$i];
        foreach my $j ($i+1..$#$val) {
            my $two = $$val[$j];
            # Calculate antinode coordinates
            my $delta_row = $$two[0] - $$one[0];
            my $delta_col = $$two[1] - $$one[1];
            my $a1 = [ $$one[0] - $delta_row, $$one[1] - $delta_col ];
            my $a2 = [ $$two[0] + $delta_row, $$two[1] + $delta_col ];
            # Save antinodes that are within range
            $antinodes{join("\t", @$_)}++
                foreach grep { ($$_[0] >= 0 and $$_[0] <= $max_row and
                                $$_[1] >= 0 and $$_[1] <= $max_col) } $a1, $a2;
        }
    }
}
print scalar(keys %antinodes), "\n";
