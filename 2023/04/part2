#!/usr/bin/perl

use 5.8.1;
use strict;
use warnings 'all';

my @num_cards = (0);
while (<>) {
    my ($card, $winners, $have) = m/^Card\s+(\d+):\s*([ \d]+?)\s*\|\s*([ \d]+)/;
    $num_cards[$card]++;                # Count this card

    # Get a list of winning numbers
    my (@winners) = sort { $a <=> $b } split / +/, $winners;
    # Make a hash of numbers we have
    my %have = (); @have{split / +/, $have} = ();

    # Count how many winning numbers are in the numbers we have
    my $winning_count = (grep { exists $have{$_} } @winners);

    for (my $i = 1; $i <= $winning_count; $i++) {
	$num_cards[$card + $i] += $num_cards[$card];
    }
}

my $sum = 0;
$sum += $_ foreach @num_cards;
print "${sum}\n";
