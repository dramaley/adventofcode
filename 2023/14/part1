#!/usr/bin/perl

use 5.8.1;
use strict;
use warnings 'all';

my @grid = map { chomp; [ split // ] } <>;

# Move O's up. Smells like a bubble sort.
for (my $col = 0; $col < @{$grid[0]}; $col++) {
    my $bottom = 0;
    for (my $row = 0; $row < @grid; $row++) {
        if ($grid[$row][$col] eq '#') { # Found a cube-shaped rock
            $bottom = $row + 1;         # It becomes the new bottom.
            next;
        }
        if ($grid[$row][$col] eq 'O') { # Found a rolling boulder
            if ($bottom < $row) {
                $grid[$bottom][$col] = 'O'; # Roll the boulder to the bottom
                $grid[$row][$col] = '.';
            }
            $bottom++;
        }
    }
}

my $sum = 0;
$sum += (@grid - $_) * scalar(grep { /O/ } @{$grid[$_]}) foreach 0..$#grid;
print "${sum}\n";
