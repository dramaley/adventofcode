#!/usr/bin/perl

use 5.8.1;
use strict;
use warnings 'all';

# Relative value of the cards.
our %card_vals = ('J' => 1,  '2' => 2,  '3' => 3, '4' => 4, '5' => 5,
                  '6' => 6,  '7' => 7,  '8' => 8, '9' => 9, 'T' => 10,
                  'Q' => 11, 'K' => 12, 'A' => 13);

# ------------------------------------------------------------------------------
#   Given a hand, return its ranking
# ------------------------------------------------------------------------------
sub hand2ranking {
    my ($hand) = @_;

    # How many copies of each card we have
    my %counts = ();
    $counts{$_}++ foreach split //, $hand;
    # Handle jokers
    if ($counts{'J'} and $counts{'J'} != 5) {
        my $jokers = delete $counts{'J'};
        my $highest = (sort { $counts{$a} <=> $counts{$b} } keys(%counts))[-1];
        $counts{$highest} += $jokers;
    }

    # Associate how many copies of each card we have with a count of how many
    # types of those cards there are.
    my @types = (0) x 6;
    $types[$_]++ foreach values %counts;

    # Figure out what type of hand we have
    SWITCH: {
        $types[5]               && do { return 6; }; # Five of a kind
        $types[4]               && do { return 5; }; # Four of a kind
        $types[3] and $types[2] && do { return 4; }; # Full house
        $types[3]               && do { return 3; }; # Three of a kind
        $types[2] == 2          && do { return 2; }; # Two pair
        $types[2]               && do { return 1; }; # One pair
        1                       && do { return 0; }; # High card
    }
}


# Read the input
my @data = ();
while (<>) {
    chomp;
    my ($hand, $bid) = split / +/;
    # Convert the hand into hexadecimal notation for faster sorting later
    my $hex = sprintf('%x%x%x%x%x', map { $card_vals{$_} } split //, $hand);
    # Determine this hand's type
    my $type = &hand2ranking($hand);

    push @data, { 'bid' => $bid, 'hand' => $hand,
                  'hex' => $hex, 'type' => $type };
}

# Sort hands, first by type of hand, then by hex value of the hand
@data = sort { ($a->{'type'} <=> $b->{'type'} or
                $a->{'hex'} cmp $b->{'hex'})     } @data;

# Figure out the winnings
my $sum = 0;
$sum += ($_ + 1) * ${data[$_]}{'bid'} foreach 0..$#data;
print "${sum}\n";
