#!/usr/bin/perl

use 5.8.1;
use strict;
use warnings 'all';

our %cache = ();

# ------------------------------------------------------------------------------
#  Given a string and a list of contiguous groups, figure out how many possible
#  arrangements there are. Possible characters in the string and their meaning:
#    .   Not part of a group
#    #   Part of a group
#    ?   Unknown / wildcard
# ------------------------------------------------------------------------------
sub solveit {
    my ($list, @groups) = @_;
    my $cachekey = join('-', map { defined $_ ? $_ : '' } @_);

    # We've been here before
    return $cache{$cachekey} if exists $cache{$cachekey};

    # Minimum number of characters we need for a match.
    my $min = 0;
    $min += $_ foreach @groups;
    $min += @groups - 1;

    my $retval = 0;
    my $group = shift @groups;          # How long the next group needs to be
    for (my $i = 0; $i <= length($list) - $min; $i++) {
        if ($list =~ m/^[^#]{${i}}[?#]{${group}}(?!#)/) {
            my $regex = '^.{' . ($group + $i) . '}.?';
            # Remove what we matched plus padding (if any)
            (my $remainder = $list) =~ s/${regex}//;
            if (@groups) {
                $retval += &solveit($remainder, @groups);
            } else {
                $retval += $remainder =~ m/^[?.]*$/ ? 1 : 0;
            }
        }
    }
    $cache{$cachekey} = $retval;
    return $retval;
}


# Read input and process each line as we go.
my $sum = 0;
while (<>) {
    chomp;
    my $original = $_;
    my ($list, @damaged) = split /[ ,]/;

    # Embiggen everything
    $list = join('?', ($list) x 5);
    @damaged = (@damaged) x 5;

    $sum += &solveit($list, @damaged);
}
print "${sum}\n";
